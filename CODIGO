#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <LiquidCrystal_I2C.h>

#define TRIG_PIN 5
#define ECHO_PIN 18

const char* SSID = "Wokwi-GUEST";
const char* PASSWORD = "";
const char* TAGO_DEVICE_TOKEN = "c5e8232c-ed2a-43e3-ae88-c678e59524a5";

LiquidCrystal_I2C lcd(0x27, 16, 2);

unsigned long lastSend = 0;
const int SEND_INTERVAL = 5000;

void setup() {
  Serial.begin(115200);
  Serial.println("Iniciando...");  
  lcd.init();
  lcd.backlight();

  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);

  WiFi.begin(SSID, PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nWi-Fi conectado!");
}

float readDistance() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  float duration = pulseIn(ECHO_PIN, HIGH);
  return duration * 0.0344 / 2;
}

String simulateRFID() {
  return "12345678";
}

void sendToTago(String rfid, float dist, String status) {
  WiFiClientSecure client;
  client.setInsecure(); // ignora verificação SSL (requerido no Wokwi)

  HTTPClient http;
  http.begin(client, "https://api.tago.io/data");
  http.addHeader("Content-Type", "application/json");
  http.addHeader("Device-Token", TAGO_DEVICE_TOKEN);

  String payload = "[";
  payload += "{\"variable\":\"rfid\",\"value\":\"" + rfid + "\"},";
  payload += "{\"variable\":\"distancia\",\"value\":" + String(dist) + "},";
  payload += "{\"variable\":\"status\",\"value\":\"" + status + "\"}";
  payload += "]";

  int code = http.POST(payload);
  Serial.println("Enviado para TagoIO. Código HTTP: " + String(code));
  Serial.println("Payload: " + payload);

  http.end();
}

void loop() {
  if (millis() - lastSend > SEND_INTERVAL) {
    lastSend = millis();

    float dist = readDistance();
    String rfid = simulateRFID();
    String status = (dist <= 20) ? "Cheio" : (dist <= 150) ? "Médio" : "Vazio";

    sendToTago(rfid, dist, status);

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("ID: " + rfid);
    lcd.setCursor(0, 1);
    lcd.print("Est: " + status);
  }
}
